apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
apply plugin: 'jacoco'

defaultTasks 'build'

group = "com.toastedbits.jacoco"
version = "1.0-SNAPSHOT"

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

buildDir = 'target'

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.testng:testng:6.8.5'
	providedCompile 'org.apache.tomcat:servlet-api:6.0.37'
}

test {
	useTestNG() {
		suiteXmlBuilder().suite(name: 'jacoco demo test suite', parallel: 'tests') {
			test (name: 'all-tests') {
				packages {
					'package' (name:'com.toastedbits.jacoco.test.*')
				}
			}
		}
	}
	ext.useDefaultListeners = true
}

eclipse {
    wtp {
		//By default gradle excludes provided dependencies with the default value for minusConfigurations,
		//We want these dependencies to appear in our eclipse project since we dont run eclipse from within the tomcat environment
        component {
            plusConfigurations += configurations.providedRuntime
            minusConfigurations = null
        }
		//The default facet configuration generated by the eclipse-wtp plugin defaults to outdated values 
		//This causes unusual phony errors to be reported by eclipse when using jdk 1.7 features, let's bring them up to date
        facet {
            facet name: 'jst.web', version: '3.0'
            facet name: 'java', version: '1.7'
            facet name: 'wst.jsdt.web', version: '1.0'
        }
    }
}

war {
	archiveName = project.name + ".war"
}
